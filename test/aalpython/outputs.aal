aal "outputs" {
    language: python { import os }

    variables { found, exists, filenames, file_exists }

    initial_state {
        pre   = "/tmp/fmbt.aalpython.file."
        filenames = [pre+"txt", pre+"jpg", pre+"png", pre+"tmp"]
        found = {'txt': False, 'jpg': False, 'png': False, 'tmp': False}
        exists = {'txt': False, 'jpg': False, 'png': False, 'tmp': False}
        file_exists = os.path.exists
        for filename in filenames:
            if file_exists(filename): os.remove(filename)
    }

    action "iChange:txt", "iChange:jpg", "iChange:png", "iChange:tmp" {
        guard() {
            # Enable iChange:X for non-existing file.X only -
            # unless all files exist.
            suffix = action_name.split(":")[1]
            log("iChange" + suffix + " exists:" + str(exists[suffix]))
            if not exists[suffix]: return True
            if exists.values() == [True, True, True, True]: return True
            return False
        }
        body()  {
            suffix = action_name.split(":")[1]
            log("iChange with file suffix %s" % (suffix,))
            exists[suffix] = not exists[suffix]
        }
        adapter() {
            if not file_exists(filenames[action_index]):
                file(filenames[action_index],"w")
            else: os.remove(filenames[action_index])
        }
    }

    action "o:Txt changed" {
        guard() { return True }
        body() {
            found['txt'] = not found['txt']
        }
        adapter() {
            if file_exists(filenames[0]) != found['txt']: return True
        }
    }

    action "o:Jpg changed" {
        body() { found['jpg'] = not found['jpg'] }
    }
    action "o:Png changed" {
        guard() { return 1 }
        body() {
            found['png'] = not found['png']
        }
        adapter() {
            if file_exists(filenames[1]) != found['jpg']:
                return action("o:Jpg changed")
            elif file_exists(filenames[2]) != found['png']:
                return action("o:Png changed")
        }
    }

    action "o:Tmp changed" {
        body() {
            found['tmp'] = not found['tmp']
        }
        adapter() {
            if file_exists(filenames[3]) != found['tmp']:
                return "o:Tmp changed"
        }
    }
}
